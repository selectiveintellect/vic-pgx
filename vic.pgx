%grammar vic
%version 0.0.2
#COPYRIGHT: 2014 Vikas N Kumar <vikas@cpan.org>. All Rights Reserved

# uc-select is necessary.
program: uc-select header* statement* <EOS>

header: uc-config | comment
uc-select: /PIC <BLANK>+ (<uc-types>) <SEMI> <EOL>/

# P16F690X is fake just to show how to enumerate.
uc-types: /(?i:P16F690 | P16F690X)/
uc-config: /config <BLANK>+ (<ANY>*) <SEMI> <EOL>/
comment: /<BLANK>* <HASH> <ANY>* <EOL>/ | blank-line
blank-line: whitespace* /<EOL>/
whitespace: /<BLANK>+/

statement: comment | start-block | instruction | expression | end-block

start-block: name whitespace* <LCURLY> whitespace* <EOL>?
end-block: whitespace* <RCURLY> whitespace* <EOL>?

instruction: name values* whitespace* <SEMI> <EOL>?

name: whitespace* identifier whitespace*
values: value-comma* value
value-comma: value /<COMMA>/
value: whitespace* (string | number-units | number | variable) whitespace*

expression: whitespace* variable whitespace* operator value <SEMI> <EOL>?

string: single-quoted-string | double-quoted-string

# most microcontrollers cannot do floating point math so ignore real numbers
number-units: number whitespace* units
# number handles both hex and non-hex values for ease of use
number: /(0x<HEX>+ | 0X<HEX>+ | <DIGIT>+)/
units: /(s | ms | us)/

variable: <DOLLAR> identifier
identifier: /(<ALPHA>[<WORDS>]*)/

operator: /([=])/

single_quoted_string:
    /(:
        <SINGLE>
        ((:
            [^<BREAK><BACK><SINGLE>] |
            <BACK><SINGLE> |
            <BACK><BACK>
        )*?)
        <SINGLE>
    )/

double_quoted_string:
    /(:
        <DOUBLE>
        ((:
            [^<BREAK><BACK><DOUBLE>] |
            <BACK><DOUBLE> |
            <BACK><BACK> |
            <BACK><escape>
        )*?)
        <DOUBLE>
    )/

escape: / [0nt] /
